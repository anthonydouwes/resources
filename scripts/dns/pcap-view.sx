include "./dns-udf.sx";

@list    = LIST('https://logsamples.spectx.com/dns/dns1.pcap');
@stream  = PARSE(pattern:'PCAP', src:@list);

@dns = @stream
 .filter((PORT(ipDst) = 53 or PORT(ipSrc) = 53))    //select only DNS traffic
 .select(*, data:$parseDnsMessage(_raw_bytes))      //parse DNS payload
 .select(*, query:UNNEST(data[queries]),            //since Query and Answer sections contain array
		   answer:UNNEST(data[answers]))            //expand them to rows
 .filter(answer is not NULL)                        //leave only records with both query and answer data
;

@normalize = @dns
 .select(                                           //create normalized view, similar to dns server log entry
    timestamp:captureTime
   ,clientIp:IPADDR(ipSrc)
   ,txId:data[txId]
   ,flags:data[flags]
   ,opCode:$getOpCode(data[opCode])
   ,authoritative:data[isAuthoritative]
   ,truncated:data[isTruncated]
   ,recursionDesired:data[recursionDesired]
   ,recursionAvailable:data[recursionAvailable]
   ,response:$getResponse(data[rCode])
   ,qType:$getRRType(query[qType])
   ,qName:query[qName]
   ,qClass:$getClassType(query[qClass])
   ,rrType:$getRRType(answer[type])
   ,rrClass:$getClassType(answer[class])
   ,ttl:answer[ttl]
   ,rData:$getRdata(answer[rdata])
 )
;

@normalize;