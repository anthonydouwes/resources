// $[/shared/patterns/aws_s3.sxp]
// https://docs.aws.amazon.com/AmazonS3/latest/dev/LogFormat.html
// NOTE: Any field can be set to - to indicate that the data was unknown
//       or unavailable, or that the field was not applicable to this request

$s3_access_base=( // from year 2012
  NSPACE:bucket_owner
  NSPACE:bucket
  TIMESTAMP('[dd/MMM/yyyy:HH:mm:ss Z]'):time
  ('-' | IPADDR:remote_ip)
  ('-' | NSPACE:requester)
  ('-' | NSPACE:request_id)
  ('-' | NSPACE:operation)
  ('-' | NSPACE:key)
  ('-' | '"-"' | ('"' LD:request_verb ' ' LD:uri ' ' LD:protocol '"'))
  ('-' | INT:http_status)
  ('-' | NSPACE:error_code)
  ('-' | LONG:bytes_sent)
  ('-' | LONG:object_size)
  ('-' | LONG:total_time)
  ('-' | LONG:turn_around_time)
  ('-' | '"-"' | DQS:referrer)
  ('-' | '"-"' | DQS:user_agent)
  ('-' | NSPACE:version_id)
)(fs=' ');

$s3_extra_march_2019=
  ' ' ('-' | NSPACE:host_id)
  ' ' ('-' | ('SigV2' | 'SigV4'):signature_version)
  ' ' ('-' | NSPACE:cipher_suite)
  ' ' ('-' | NSPACE:authentication_type)
  ' ' ('-' | NSPACE:host_header)
  ' ' ('-' | ('TLS' NSPACE):tls_version)
;

/*
	between 2019-02-05 and 2019-05-01 S3 logs may contain records with
	a) one extra field between "tls_version" and "host_id"
	b) missing tls_version
*/

$s3_temporary_broken_logs_match_2019=
  ' ' ('-' | NSPACE:unknwon_1)
  (
    ' ' ('-' | NSPACE:host_id)
    ' ' ('-' | ('SigV2' | 'SigV4'):signature_version)
    ' ' ('-' | NSPACE:cipher_suite)
    ' ' ('-' | NSPACE:authentication_type)
    ' ' ('-' | NSPACE:host_header)
    (
	  ' ' ('-' | ('TLS' NSPACE):tls_version)
	)?
  )?
  >>EOL // extra lookahead for safety
;

// root pattern
$s3_access_base ($s3_extra_march_2019 | $s3_temporary_broken_logs_match_2019)? EOL